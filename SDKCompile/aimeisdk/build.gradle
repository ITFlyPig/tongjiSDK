apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

// 提交到仓库中的版本号
version = "1.0.0"

repositories {
    mavenCentral()
    maven { url 'http://maven.mogujie.org/nexus/content/groups/mitpublic/' }
    maven { url 'http://maven.mogujie.org/nexus/content/repositories/ThousandSunny' }
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {

        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.0'
    compile 'com.mogujie.vegetaglass:vegetaglass:3.0.8@aar'
}

def siteUrl = 'https://github.com/ITFlyPig/tongjiSDK'      // 项目的主页
def gitUrl = 'https://github.com/ITFlyPig/tongjiSDK.git'   // Git仓库的url
group = "com.wangyuelin.android"
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'aimeizhuyi Android tongji sdk'    //项目描述
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'wangyuelin'        //填写的一些基本信息
                        name 'wangyuelin'
                        email 'a1659509224@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "aimeitongjiSDK"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}